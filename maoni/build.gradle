/*
 * Copyright (c) 2016 Armel Soro
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 600
        versionName "6.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
//        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

group = 'org.rm3l'
version = '6.0.0'

final homePath = System.properties['user.home']

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":maoni-common")
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'me.panavtec:drawableview:0.6.0'

    //Include maoni-email as the default listener
    implementation 'org.rm3l:maoni-email:3.1.0-maoni_6.0.0'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

bintray {

    //Attempt to read keystore.properties file, if any first
    final propsFile = new File(homePath.toString() + "/.droid/", "maoni.bintray.properties")

    if (propsFile.exists()) {
        final props = new Properties()
        props.load(new FileInputStream(propsFile))
        user = props['user']
        key = props['key']
    } else {
        user = ""
        key = ""
    }

    publish = true //If version should be auto published after an upload
    
    pkg {
        repo = 'maven'
        name = 'org.rm3l:maoni'
        labels = ['maoni', 'android', 'feedback', 'android-lib', 'comments', 'review', 'bug', 'issue']

        //noinspection GroovyAssignabilityCheck
        version {
            name = '6.0.0'
            desc = 'Maoni Android Library'
            released  = new Date()
            vcsTag = '6.0.0'
        }

        publicDownloadNumbers = true

        licenses = ['MIT']
        vcsUrl = 'https://github.com/rm3l/maoni.git'
        websiteUrl = 'https://rm3l.github.io/maoni'
        issueTrackerUrl = 'https://github.com/rm3l/maoni/issues'

    }
    configurations = ['archives']
}
